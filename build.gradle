plugins {
    id 'maven'
    id 'java'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'net.researchgate.release' version '2.6.0'
    id 'org.xtext.xtend' version '1.0.17'
}

repositories {
    jcenter()
}

group = 'org.testeditor.fixture.host'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// show standard out during test to see logging output
test.testLogging.showStandardStreams = true


dependencies {
    compile 'org.testeditor.fixture:core-fixture:3.2.0'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.guava:guava:21.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'com.github.tomakehurst:wiremock:2.6.0'
    testCompile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.11.0'
    testCompile 'com.google.truth:truth:0.31'
    testCompile 'commons-codec:commons-codec:1.10'
    testCompile 'org.apache.commons:commons-lang3:3.6'
    
}
    
sourceSets.main.resources.srcDirs = [ "src/main/java" ]
sourceSets.main.resources.includes = [ "**/*.aml" ]    
 
// TODO code below copied from core-fixture
jar {
    manifest {
        attributes 'Implementation-Title': "${project.group}.${project.name}",
                   'Implementation-Version': project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.packaging = 'jar'
    }
}

release {
    preTagCommitMessage = '[release]'
    tagCommitMessage = '[release]'
    newVersionCommitMessage = '[release] new version'
    tagTemplate = 'v${version}'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true 
    pkg {
        repo = 'Fixtures'
        name = project.name
        userOrg = 'test-editor'
        licenses = ['EPL-1.0']
        vcsUrl = "https://github.com/test-editor/${project.name}.git"
        version {
            name = project.version
            vcsTag = "v$project.version"
        }
        websiteUrl = 'http://testeditor.org'
    }
}
